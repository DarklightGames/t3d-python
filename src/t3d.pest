BEGIN = _{ "Begin" }
END = _{ "End" }
NON_DIGIT_ID_CHARACTER = _{ ASCII_ALPHA | "_" }
ID_CHARACTER = _{ ASCII_DIGIT | NON_DIGIT_ID_CHARACTER }
REFERENCE_PATH_CHARACTER = _ { !WS ~ !"'" ~ !"\"" ~ ASCII }
id = { ID_CHARACTER+ }
WS = _{ " " | "\t" | "\n" }
int = { ("+" | "-")? ~ ASCII_DIGIT+ ~ !NON_DIGIT_ID_CHARACTER }
string = { (!"\"" ~ ANY)* }
string_outer = _{ "\"" ~ string ~ "\"" }
float = { ("+" | "-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ ~ ("e" ~ int)? }

reference_path = { REFERENCE_PATH_CHARACTER+ }
reference = { id ~ "'" ~ reference_path ~ "'" }

// TODO: does not handle empty entries yet.
array = { "(" ~ value ~ ("," ~ value)* ~ ")" }

value = { array | struct_ | float | int | reference | reference_path | string_outer }

array_index = _{ "(" ~ int ~ ")" }
struct_properties = _{ property_assignment ~ WHITE_SPACE* ~ ("," ~ WHITE_SPACE* ~ property_assignment)* }
struct_ = { "(" ~ struct_properties ~ ")" }

property_assignment = { id ~ array_index? ~ WHITE_SPACE* ~ "=" ~ (NEWLINE | (WHITE_SPACE* ~ value)) }
property_assignment_float_list = { float ~ WHITE_SPACE* ~ ("," ~ WHITE_SPACE* ~ float)* }
property_assignment_vector = { id ~  WHITE_SPACE+ ~ property_assignment_float_list }
property_assignment_inline_struct = { id ~ WS+ ~ property_assignment ~ (WS+ ~ property_assignment)* }

object_statement = { object | property_assignment | property_assignment_vector | property_assignment_inline_struct }
object_statements = { object_statement ~ (WHITE_SPACE+ ~ object_statement)* }
object = { BEGIN ~ WHITE_SPACE+ ~ id ~ (WHITE_SPACE+ ~ object_statements)? ~ WHITE_SPACE+ ~ END ~ WHITE_SPACE+ ~ id }

objects = _{ object ~ (WHITE_SPACE+ ~ object)* }

t3d = { SOI ~ WHITE_SPACE* ~ objects ~ WHITE_SPACE* ~ EOI }
